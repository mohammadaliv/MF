       ID DIVISION.
       PROGRAM-ID. ACCTPGM1.
       AUTHOR. NAME.
       DATE-WRITTEN. TODAY.

       ENVIRONMENT DIVISION.
       INPUT-OUTPUT SECTION.
       FILE-CONTROL.
           SELECT  ACCT-FILE   ASSIGN  TO ACCTDD01
           ORGANIZATION  IS  SEQUENTIAL
           ACCESS  MODE  IS  SEQUENTIAL
           FILE STATUS   IS  WS-OUTF-STATUS.

       DATA DIVISION.
       FILE SECTION.
       FD ACCT-FILE
           LABEL RECORD ARE STANDARD.
       COPY ACCTCPY1
               REPLACING  ACCT-DETAILS  BY  ACCT-DETAILS-OUT.
       WORKING-STORAGE SECTION.
       01 WS-OUTF-STATUS   PIC   X(02)  VALUE SPACE.
       01 ACCT-DETAILS-IN.
          03 ACCT-NUMBER   PIC    X(05)  VALUE  SPACE.
          03 CUST-NAME     PIC    X(10)  VALUE  SPACE.
          03 ACCT-BAL      PIC   S9(03)V9(02) VALUE ZERO.
          03 ACCT-TYPE     PIC    X(10)  VALUE  SPACE.

       01  WS-ANY-MORE-INPUT  PIC   X(01)  VALUE SPACE.
       01  WS-VALID-INPUT     PIC   X(01)  VALUE SPACE.
       01  WS-IN-REC-CNT      PIC   9(02)  VALUE ZERO.
       01  WS-OT-REC-CNT      PIC   9(02)  VALUE ZERO.
       01  WS-ABENDPGM        PIC   X(08)  VALUE 'ABENDPGM'.

       PROCEDURE DIVISION.
       000-MAIN-PARA.
           DISPLAY  'ACCTPGM1 STARTED'.

           PERFORM  100-INITIAL-PARA   THRU 100-EXIT
           PERFORM  200-GET-INPUT-PARA THRU 200-EXIT
           PERFORM  300-PROCESS-PARA  THRU 300-EXIT
                           UNTIL  WS-ANY-MORE-INPUT = 'N'.

           CLOSE  ACCT-FILE.
           DISPLAY  'TOTAL  INPUT  RECORD COUNT '  WS-IN-REC-CNT.
           DISPLAY  'TOTAL  OUTPUT RECORD COUNT '  WS-OT-REC-CNT.
           STOP RUN.

       100-INITIAL-PARA.
           MOVE  'Y'    TO   WS-ANY-MORE-INPUT.
           MOVE  ZERO   TO  WS-IN-REC-CNT  WS-OT-REC-CNT.

           OPEN OUTPUT ACCT-FILE.
           IF WS-OUTF-STATUS = '00'
               CONTINUE
           ELSE
              DISPLAY  'ERROR IN 100-PARA'
              DISPLAY  'FILE OPEN ERROR STATUS IS  ' WS-OUTF-STATUS
              CALL WS-ABENDPGM
           END-IF.
           INITIALIZE  ACCT-DETAILS-IN  ACCT-DETAILS-OUT.
       100-EXIT. EXIT.

       200-GET-INPUT-PARA.
           ACCEPT ACCT-NUMBER  OF ACCT-DETAILS-IN
           ACCEPT CUST-NAME    OF ACCT-DETAILS-IN
           ACCEPT ACCT-BAL     OF ACCT-DETAILS-IN
           ACCEPT ACCT-TYPE    OF ACCT-DETAILS-IN.
           ADD +1   TO  WS-IN-REC-CNT.
       200-EXIT. EXIT.

       300-PROCESS-PARA.
           MOVE  'Y'   TO  WS-VALID-INPUT.
           PERFORM  310-AUDIT-INPUT-PARA   THRU 310-EXIT.

           IF WS-VALID-INPUT = 'Y'
              PERFORM  340-MOVE-WRITE-PARA  THRU 340-EXIT
           END-IF.

           ACCEPT  WS-ANY-MORE-INPUT
           IF WS-ANY-MORE-INPUT  = 'Y'
              PERFORM  200-GET-INPUT-PARA  THRU 200-EXIT
           END-IF.
       300-EXIT. EXIT.

       310-AUDIT-INPUT-PARA.
           IF ACCT-NUMBER OF ACCT-DETAILS-IN = SPACE
               MOVE  'N'   TO  WS-VALID-INPUT
               GO TO 310-EXIT
           END-IF.

           IF CUST-NAME   OF ACCT-DETAILS-IN = SPACE
               MOVE  'N'   TO  WS-VALID-INPUT
               GO TO 310-EXIT
           END-IF.

           IF ACCT-BAL    OF ACCT-DETAILS-IN < 0
               MOVE  'N'   TO  WS-VALID-INPUT
               GO TO 310-EXIT
           END-IF.

           IF ACCT-TYPE   OF ACCT-DETAILS-IN = SPACE
               MOVE  'N'   TO  WS-VALID-INPUT
           END-IF.
       310-EXIT. EXIT.

       340-MOVE-WRITE-PARA.
           MOVE  ACCT-NUMBER OF ACCT-DETAILS-IN  TO
                  ACCT-NUMBER OF ACCT-DETAILS-OUT.
           MOVE  CUST-NAME   OF ACCT-DETAILS-IN  TO
                  CUST-NAME   OF ACCT-DETAILS-OUT.
           MOVE  ACCT-BAL    OF ACCT-DETAILS-IN  TO
                  ACCT-BAL    OF ACCT-DETAILS-OUT.
           MOVE  ACCT-TYPE   OF ACCT-DETAILS-IN  TO
                  ACCT-TYPE   OF ACCT-DETAILS-OUT.
           WRITE ACCT-DETAILS-OUT.
           IF  WS-OUTF-STATUS = '00'
               ADD +1   TO  WS-OT-REC-CNT
           ELSE
               DISPLAY 'ERORR IN 340-MOVE-WRITE-PARA'
               DISPLAY 'WRITE ERROR STATUS IS  ' WS-OUTF-STATUS
               DISPLAY 'RECORD KEY IS'  ACCT-NUMBER OF ACCT-DETAILS-IN
               CALL WS-ABENDPGM
           END-IF.
       340-EXIT. EXIT.
